/*

In this exercise you'll configure signing and deploy a release build.

1. Use the Build > Generate Signed APK dialog to create a new keystore (or you can use a previously generated keystore).

2. Create a signing configuration using your keystore. You can either use the Module Settings dialog (which you can access by right clicking on the app folder), or by manually configuring the signingConfigs block. Make sure the keystore is included in your project directory and is referenced via a

http://tools.android.com/tech-docs/new-build-system/user-guide#TOC-Signing-Configurations

3. Configure the release build type to use your new signing configuration. Again, you can either use the Module Settings dialog, or do it manually.

4. Finally, deploy the release build type to your test device!

The solution to this exercise is essentially Demo 4.05

*/


apply plugin: 'com.android.application'

// I added the following per the guidance of the "Remove signing information from your build files"
// section of the following link:
// https://developer.android.com/studio/publish/app-signing.html

// Creating a variable that will be initialized to my keystore.properties file in the root project
// folder:
def keystorePropertiesFile = rootProject.file("keystore.properties")

// Then, we create a Properties object based on the keystore properties file:
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    signingConfigs {
        config {
            // Finally, we can access the properties of the keystore properties file in the
            // following way
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    compileSdkVersion 24
    buildToolsVersion "24.0.1"
    defaultConfig {
        applicationId "com.udacity.gradle.automaticsigning"
        minSdkVersion 15
        targetSdkVersion 24
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:appcompat-v7:24.1.1'
}
